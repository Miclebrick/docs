
plugins {
    id 'java'
    id "org.jetbrains.kotlin.jvm" version "1.2.51"
    id "ru.endlesscode.bukkitgradle" version "0.8.0"
    id "com.github.johnrengelman.shadow" version "2.0.4"
}

group 'com.miclesworkshop.docs'
version '1.0-SNAPSHOT'

sourceCompatibility = 1.8

bukkit {
    meta {
        name = "Docs"
        description = "Plugin component of the Docs tool, a system for generating both web and ingame documentation from one source for Minecraft servers"
        main = "com.miclesworkshop.docs.DocsPlugin"
        url = "https://www.miclesworkshop.com" // Attribute website
        authors = ["MicleBrick", "Contributors"]
    }
    run {
        core = "paper"
        eula = true
        onlineMode = false
        debug = true
        encoding = "UTF-8"
        javaArgs = "-Xmx1G"
        bukkitArgs = ""
    }
}

repositories {
    mavenCentral()
    maven { url = "https://repo.aikar.co/content/groups/aikar/" }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://repo.eclipse.org/content/groups/releases/' }
}

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.2.51"
    compile "co.aikar:acf-paper:0.5.0-SNAPSHOT"
    compile "org.eclipse.jgit:org.eclipse.jgit:5.0.1.201806211838-r"
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
    kotlinOptions.javaParameters = true
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

shadowJar {
    relocate 'co.aikar.commands', 'com.miclesworkshop.docs.lib.acf'
}

build.dependsOn shadowJar